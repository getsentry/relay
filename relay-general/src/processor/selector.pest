WHITESPACE = _{ " " }

ObjectType = @{ "$" ~ ('a' .. 'z')+ }
Wildcard = @{ "*" }
DeepWildcard = @{ "**" }

Quote = _{ "'" }
EscapedQuote = @{ Quote ~ "'" }

UnquotedKey = @{ (ASCII_ALPHANUMERIC | "-" | "_") + }
RootUnquotedKey = { SOI ~ UnquotedKey ~ EOI }
QuotedKey = @{ (((!"'") ~ ANY) | EscapedQuote)+ }
Key = { UnquotedKey | Quote ~ QuotedKey ~ Quote }

Index = @{ ASCII_DIGIT+ }

SelectorPathItem = { ObjectType | DeepWildcard | Wildcard | Index | Key }
SelectorPath = { SelectorPathItem ~ ("." ~ SelectorPathItem)* }

// enforce parenthesis everywhere as long as we don't have clear precedence
// rules, such that we can introduce them without breaking existing configs.

AndSelector = { "(" ~ Selector ~ ("&" ~ Selector)+ ~ ")" }
OrSelector = { "(" ~ Selector ~ ("|" ~ Selector)+ ~ ")" }
NotSelector = { "(~" ~ Selector ~ ")" }
Selector = { NotSelector | AndSelector | OrSelector | "(" ~ Selector ~ ")" | SelectorPath }
RootSelector = { SOI ~ Selector ~ EOI }

