WHITESPACE = _{ " " }

ObjectType = @{ "$" ~ ('a' .. 'z')+ }
Wildcard = @{ "*" }
DeepWildcard = @{ "**" }

Quote = _{ "'" }
EscapedQuote = @{ "'" }

UnquotedKey = @{ (ASCII_ALPHANUMERIC | "-" | "_") + }
RootUnquotedKey = { SOI ~ UnquotedKey ~ EOI }
QuotedCharacter = @{ ((!"'") ~ ANY) }
QuotedKey = ${ (QuotedCharacter | ("'" ~ EscapedQuote))+ }
Key = { UnquotedKey | Quote ~ QuotedKey ~ Quote }
KeySubstring = { "*" ~ Key ~ "*" }

Index = @{ ASCII_DIGIT+ }

SelectorPathItem = { ObjectType | Index | Key | KeySubstring | DeepWildcard | Wildcard }
SelectorPath = { SelectorPathItem ~ ("." ~ SelectorPathItem)* }

// enforce parenthesis everywhere as long as we don't have clear precedence
// rules, such that we can introduce them without breaking existing configs.

AndSelector = { "(" ~ Selector ~ ("&" ~ Selector)+ ~ ")" }
OrSelector = { "(" ~ Selector ~ ("|" ~ Selector)+ ~ ")" }
NotSelector = { "(~" ~ Selector ~ ")" }
Selector = { NotSelector | AndSelector | OrSelector | "(" ~ Selector ~ ")" | SelectorPath }
RootSelector = { SOI ~ Selector ~ EOI }

